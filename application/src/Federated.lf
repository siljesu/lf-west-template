// This test connects a simple counting source to tester that checks against its
// own count.
target C {
    platform: {
       name: Zephyr,
       board: mimxrt1170_evk_cm7,
       port: /dev/enp4s0f2,
    },
    threading: true,
    workers: 2,
    no-compile: true,
    timeout: 10 sec,
    coordination: decentralized,
}

reactor Source(period: time(2 sec)) {
    output y: int
    timer t(1 sec, period)
    state count: int(0)

    reaction(t) -> y {=
        (self->count)++;
        lf_set(y, self->count);
    =}
}

reactor Test {
    input x: int
    state count: int(0)

    reaction(x) {=
        (self->count)++;
        printf("Received %d\n", x->value);
        if (x->value != self->count) {
            printf("FAILURE: Expected %d\n", self->count);
            exit(1);
        }
    =}
}

federated reactor at 192.0.2.1:15047 {
    s = new Source();
    d = new Test();
    s.y -> d.x;
}
